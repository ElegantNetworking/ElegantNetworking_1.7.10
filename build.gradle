buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.github.CDAGaming:ForgeGradle:1c670759c5'
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.9'
    }
}

apply plugin: 'forge'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = targetCompatibility = '1.8'

version = modVersion
group = modGroup
archivesBaseName = modBaseName


minecraft {
    version = project.forgeVersion
    runDir = "run"
    mappings = project.mcpVersion
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.8'
    compile project(':ElegantNetworkingCommon')
    compileOnly project(':ElegantNetworkingAnnotationProcessor')
    //apt 'org.projectlombok:lombok:1.18.8'
    //apt project(':ElegantNetworkingAnnotationProcessor')
}

compileJava {
    options.compilerArgs += [
            '-Aelegantnetworking.printDetails'
    ]
}

jar {
    if (!testBuild)
        exclude('hohserg/elegant/networking/test/**')
}

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                // replace version and mcversion
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    exclude('hohserg/elegant/networking/test/**')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    exclude('hohserg/elegant/networking/test/**')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { project.hasProperty("signing.keyId") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            repository(url: releasesRepoUrl/*'file://localhost/' + new File(project.getProjectDir(),'repo').getAbsolutePath()*/) {
                if (project.hasProperty("ossrhUsername"))
                    authentication userName: "$ossrhUsername", password: "$ossrhPassword"
            }

            pom.artifactId = 'elegant-networking-1.7.10'
            pom.project {
                name 'ElegantNetworking MinecraftForge 1.7.10 library'
                packaging 'jar'
                description 'This is a runtime library of ElegantNetworking for MinecraftForge 1.7.10'
                url 'https://github.com/ElegantNetworking/ElegantNetworkingRoot'

                scm {
                    connection = 'scm:git:https://github.com/ElegantNetworking/ElegantNetworking_1.7.10.git'
                    developerConnection = 'scm:git:git@github.com:https://github.com/ElegantNetworking/ElegantNetworking_1.7.10.git'
                    url = 'https://github.com/ElegantNetworking/ElegantNetworking_1.7.10'
                }

                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://raw.githubusercontent.com/ElegantNetworking/ElegantNetworking_1.7.10/master/LICENSE.md'
                    }
                }

                developers {
                    developer {
                        id = 'hohserg'
                        name = 'hohserg'
                        email = 'hohserg1@yandex.ru'
                    }
                }
            }
            pom.whenConfigured {
                p -> p.dependencies=[]
            }
        }
    }
}
